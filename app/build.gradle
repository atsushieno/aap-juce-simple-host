plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

kotlin {
    jvmToolchain(17)
}

android {
    compileSdk = libs.versions.android.compileSdk.get().toInteger()

    defaultConfig {
        applicationId "org.androidaudioplugin.samples.juce.simple_plugin_host"
        minSdk = libs.versions.android.minSdk.get().toInteger()
        externalNativeBuild {
            cmake {
                arguments "-DJUCEAAP_ENABLED=1",
                        "-DJUCE_DIR=${System.env.JUCE_DIR != null ? System.env.JUCE_DIR : "$projectDir/../external/AndroidPluginHost/JUCE"}",
                        "-DAAP_DIR=${System.env.AAP_DIR != null ? System.env.AAP_DIR : "$projectDir/../external/aap-core"}",
                        "-DAAP_JUCE_DIR=${System.env.AAP_JUCE_DIR != null ? System.env.AAP_JUCE_DIR : "$projectDir/../external/aap-juce"}",
                        "-DANDROID_STL=c++_shared",
                        "-DANDROID_CPP_FEATURES=exceptions rtti"
            }
        }
        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }

    sourceSets {
        main.java.srcDirs +=
            ["../external/AndroidPluginHost/external/JUCE/modules/juce_gui_basics/native/javaopt/app",
             "../external/AndroidPluginHost/external/JUCE/modules/juce_audio_devices/native/java/app"]
        main.res.srcDirs +=
            []
    }

    dependencies {
        implementation libs.androidx.core.ktx
        implementation libs.kotlin.stdlib.jdk8
        implementation libs.startup.runtime

        implementation libs.aap.core
        implementation libs.aap.ui.compose
        implementation libs.aap.ui.web
        // When debugging, enable this line (and make changes in settings.gradle.)
        /*
        implementation project(":androidaudioplugin")
        implementation project(":androidaudioplugin-ui-compose")
        implementation project(":androidaudioplugin-ui-web")
         */
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version libs.versions.cmake.get()
        }
    }

    buildFeatures {
        prefab true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    lint {
        // LAMESPEC: JuceMidiSupports.java is too error prone so that it always fails to build.
        // We cannot enable modern strict lint checks.
        abortOnError = false
    }

    ndkVersion libs.versions.ndk.get()
    namespace 'org.androidaudioplugin.samples.simple_plugin_host'
}

